1 REM THE COMPUTER LANGUAGE BENCHMARKS GAME
2 REM FANNKUCH-REDUX - OPTIMIZED
3 REM CONVERTED FROM LUA VERSION
10 REM
100 REM MAIN PROGRAM
110 rem PRINT "THE COMPUTER LANGUAGE BENCHMARKS GAME"
120 rem PRINT "FANNKUCH-REDUX"
130 rem PRINT
140 N% = 8
150 REM
160 REM CONSTANTS - KEEP IN REGISTERS TO AVOID REPEATED LOADS
170 C1% = 1
171 C2% = 2
172 C3% = 3
173 C4% = 4
174 CNEG% = -1
190 REM
200 REM ALLOCATE ARRAYS - USE INTEGER ARRAYS!
210 DIM P%(15)
220 DIM Q%(15)
230 DIM S%(15)
240 REM
300 REM INITIALIZE ARRAYS P, Q, S = [1,2,3,...,N]
310 I% = C1%
320 IF I% > N% THEN GOTO 360
330 P%(I%) = I%: Q%(I%) = I%: S%(I%) = I%
340 I% = I% + C1%
350 GOTO 320
360 REM
400 REM INITIALIZE VARIABLES
410 SIGN% = C1%
420 MAXFLIPS% = 0
430 SUM% = 0
440 REM
500 REM MAIN LOOP - COPY AND FLIP
510 Q1% = P%(C1%)
520 IF Q1% = C1% THEN GOTO 1000
530 REM
540 REM COPY P TO Q (FROM I=2 TO N) - OPTIMIZED
550 I% = C2%
560 IF I% > N% THEN GOTO 600
570 Q%(I%) = P%(I%)
580 I% = I% + C1%
590 GOTO 560
600 REM
610 REM COUNT FLIPS
620 FLIPS% = C1%
630 REM FLIP LOOP
640 QQ% = Q%(Q1%)
650 IF QQ% = C1% THEN GOTO 900
660 REM
670 Q%(Q1%) = Q1%
680 REM IF Q1 >= 4 THEN REVERSE Q(2..Q1-1) - OPTIMIZED LOOP
690 IF Q1% < C4% THEN GOTO 850
700 I% = C2%
710 J% = Q1% - C1%
720 IF I% >= J% THEN GOTO 850
730 T% = Q%(I%): Q%(I%) = Q%(J%): Q%(J%) = T%
740 I% = I% + C1%: J% = J% - C1%
750 GOTO 720
850 REM
860 Q1% = QQ%
870 FLIPS% = FLIPS% + C1%
880 GOTO 630
900 REM
910 REM UPDATE SUM AND MAXFLIPS
920 SUM% = SUM% + SIGN% * FLIPS%
930 IF FLIPS% > MAXFLIPS% THEN MAXFLIPS% = FLIPS%
940 REM
1000 REM PERMUTE
1010 IF SIGN% = C1% THEN GOTO 1100
1020 REM
1030 REM SIGN = -1: SWAP P(2) AND P(3)
1040 T% = P%(C2%): P%(C2%) = P%(C3%): P%(C3%) = T%
1050 SIGN% = C1%
1060 REM
1070 REM FOR I=3 TO N
1080 I% = C3%
1090 GOTO 1200
1100 REM
1110 REM SIGN = 1: SWAP P(1) AND P(2)
1120 T% = P%(C1%): P%(C1%) = P%(C2%): P%(C2%) = T%
1130 SIGN% = CNEG%
1140 GOTO 500
1150 REM
1200 REM CHECK S(I)
1210 IF I% > N% THEN GOTO 3000
1220 SX% = S%(I%)
1230 IF SX% <> C1% THEN S%(I%) = SX% - C1%: GOTO 500
1240 IF I% = N% THEN GOTO 3000
1250 S%(I%) = I%
1260 REM
1270 REM ROTATE P(1..I+1) LEFT - OPTIMIZED
1280 T% = P%(C1%)
1290 J% = C1%
1300 IF J% > I% THEN P%(I% + C1%) = T%: GOTO 1400
1310 P%(J%) = P%(J% + C1%)
1320 J% = J% + C1%
1330 GOTO 1300
1400 REM
1410 I% = I% + C1%
1420 GOTO 1200
1430 REM
3000 REM OUTPUT RESULTS
3010 PRINT SUM%
3020 PRINT "PFANNKUCHEN("; N%; ") = "; MAXFLIPS%
3030 rem PRINT
3040 END
